import "pkg:/source/utilities/utilities.bs"
import "pkg:/source/api/jamendoSDK.bs"
import "pkg:/source/enum/Playback/PlaybackState.bs"
import "pkg:/source/enum/Playback/PlaybackCommand.bs"

sub init()
    m.top.backgroundColor = "#262626"
    m.top.backgroundURI = ""

    m.deviceInfo = CreateObject("roDeviceInfo")

    m.audioPlayer = m.top.findNode("audioPlayer")
    m.nowPlaying = m.top.findNode("nowPlaying")
    m.disclaimer = m.top.findNode("disclaimer")

    m.audioPlayer.observeField("state", "onStateChange")
    m.audioPlayer.observeField("position", "onPositionChange")

    m.nowPlayingAnimation = m.top.findNode("nowPlayingAnimation")
    m.nowPlayingInterpolator = m.top.findNode("nowPlayingInterpolator")

    m.radioGrid = m.top.findNode("radioGrid")
    m.radioGrid.setFocus(true)
    m.radioGrid.observeField("selectedRadio", "onSelectedRadioChange")

    m.screensaverTimeout = 0
    m.screensaverActive = false

    m.getScreensaverTimeoutTask = createObject("roSGNode", "GetScreensaverTimeoutTask")
    m.getScreensaverTimeoutTask.observeField("responseBody", "onScreensaverTimeoutLoaded")
    m.getScreensaverTimeoutTask.control = "RUN"
end sub

sub onScreensaverTimeoutLoaded()
    m.getScreensaverTimeoutTask.unobserveField("responseBody")
    if not isValid(m.getScreensaverTimeoutTask.responseBody) then return

    m.screensaverTimeout = m.getScreensaverTimeoutTask.responseBody
end sub

sub activateScreensaver()
    if not m.audioPlayer.disableScreenSaver then return

    m.radioGrid.setFocus(false)
    m.top.setFocus(true)

    screenSaver = createObject("roSGNode", "ScreenSaver")
    screenSaver.id = "screenSaver"
    screenSaver.trackCover = m.audioPlayer.content.FHDPosterUrl
    m.top.appendChild(screenSaver)

    m.screensaverActive = true
end sub

sub deactivateScreensaver()
    screenSaver = m.top.findNode("screenSaver")
    if not isValid(screenSaver) then return

    m.top.removeChild(screenSaver)
    m.radioGrid.setFocus(true)

    m.screensaverActive = false
end sub

sub onPositionChange()
    m.nowPlaying.position = m.audioPlayer.position / m.audioPlayer.duration

    if not m.screensaverActive
        if m.screensaverTimeout <> 0
            if m.deviceInfo.TimeSinceLastKeypress() > m.getScreensaverTimeoutTask.responseBody
                activateScreensaver()
            end if
        end if
    end if
end sub

sub onSelectedRadioChange()
    m.progressDialog = createObject("roSGNode", "StandardProgressDialog")
    m.progressDialog.title = "Loading"
    m.progressDialog.message = "Loading radio data"
    m.top.getScene().dialog = m.progressDialog

    selectedRadioGenre = LCase(m.radioGrid.selectedRadio)

    m.getTracksTask = createObject("roSGNode", "GetTracks")
    m.getTracksTask.observeField("responseBody", "onTracksLoaded")

    m.getTracksTask.params = {
        format: "jsonpretty",
        limit: 200,
        ' lang: "en",
        ' featured: 1,
        ' speed: "veryhigh",
        type: "single+albumtrack",
        tags: selectedRadioGenre,
        groupby: "artist_id",
        audioformat: "mp32",
        imagesize: "200"
    }
    m.getTracksTask.control = "RUN"
end sub

sub noTracksFound()
    m.progressDialog.close = true

    m.noTracksDialog = createObject("roSGNode", "StandardMessageDialog")
    m.noTracksDialog.title = "No Tracks Found"
    m.noTracksDialog.message = ["Jamendo returned no tracks for this radio."]
    m.noTracksDialog.buttons = ["OK"]
    m.noTracksDialog.observeFieldScoped("buttonSelected", "closeNoTracksDialog")
    m.top.getScene().dialog = m.noTracksDialog
end sub

sub closeNoTracksDialog()
    m.noTracksDialog.close = true
end sub

sub onTracksLoaded()
    if not isValid(m.getTracksTask.responseBody)
        noTracksFound()
        return
    end if

    if isEmpty(m.getTracksTask.responseBody.results)
        noTracksFound()
        return
    end if

    ' Create an array of all possible song indexes
    indexList = []
    for i = 0 to m.getTracksTask.responseBody.results.count() - 1
        indexList.push(i)
    end for

    m.indexList = shuffleArray(indexList)

    ' Load 1st song
    m.audioPlayer.content = loadNextSong()
    m.audioPlayer.control = PlaybackCommand.Play
end sub

function shuffleArray(input as object) as object

    if isEmpty(input) then return []

    arrayToShuffle = input.slice()

    ' Shuffle song indexes
    for i = 0 to arrayToShuffle.count() - 1
        j = Rnd(arrayToShuffle.count() - 1)
        temp = arrayToShuffle[i]
        arrayToShuffle[i] = arrayToShuffle[j]
        arrayToShuffle[j] = temp
    end for

    return arrayToShuffle
end function

sub onStateChange()
    m.nowPlaying.isPaused = LCase(m.audioPlayer.state) = PlaybackState.Paused

    if LCase(m.audioPlayer.state) = PlaybackState.Playing
        m.audioPlayer.disableScreenSaver = true
        m.progressDialog.close = true

        if m.nowPlaying.translation[1] <> 600
            m.disclaimer.opacity = 0
            m.nowPlayingInterpolator.reverse = "false"
            m.nowPlayingAnimation.control = "start"
        end if
        currentSong = m.audioPlayer.content
        m.nowPlaying.trackArtist = currentSong.ShortDescriptionLine1
        m.nowPlaying.trackTitle = currentSong.title
        m.nowPlaying.trackAlbum = currentSong.album
        m.nowPlaying.trackCover = currentSong.FHDPosterUrl

        screenSaver = m.top.findNode("screenSaver")
        if isValid(screenSaver)
            screenSaver.trackCover = currentSong.FHDPosterUrl
        end if
        return
    end if

    if LCase(m.audioPlayer.state) = PlaybackState.Finished
        if not isEmpty(m.indexList)
            m.audioPlayer.content = loadNextSong()
            m.audioPlayer.control = PlaybackCommand.Play
            return
        end if
    end if

    ' There are no more songs in the queue
    if isEmpty(m.indexList)
        m.audioPlayer.disableScreenSaver = false
        m.disclaimer.opacity = 100
        m.nowPlayingInterpolator.reverse = "true"
        m.nowPlayingAnimation.control = "start"
        m.nowPlaying.trackArtist = ""
        m.nowPlaying.trackTitle = ""
        m.nowPlaying.trackAlbum = ""
        m.nowPlaying.trackCover = ""
    end if
end sub

function loadNextSong() as object
    song = m.getTracksTask.responseBody.results[m.indexList.shift()]
    songContent = createObject("RoSGNode", "ContentNode")
    songContent.ContentType = 5
    songContent.url = song.audio
    songContent.title = song.name
    songContent.streamformat = "mp3"
    songContent.ShortDescriptionLine1 = song.artist_name
    songContent.album = song.album_name
    songContent.FHDPosterUrl = song.image
    return songContent
end function

function onKeyEvent(key as string, press as boolean) as boolean
    if m.screensaverActive
        deactivateScreensaver()
    end if

    if not press then return false

    if LCase(key) = "play"
        m.audioPlayer.control = LCase(m.audioPlayer.state) = PlaybackState.Paused ? PlaybackCommand.Resume : PlaybackCommand.Pause
        return true
    end if

    return false
end function
