import "pkg:/source/utilities/utilities.bs"
import "pkg:/source/api/jamendoSDK.bs"

sub init()
    m.top.backgroundColor = "#262626" '"#101010"
    m.top.backgroundURI = ""

    m.audioPlayer = m.top.findNode("audioPlayer")
    m.audioPlayer.observeField("state", "onStateChange")

    m.artist = m.top.findNode("artist")
    m.title = m.top.findNode("title")
    m.album = m.top.findNode("album")
    m.cover = m.top.findNode("cover")

    m.radioGrid = m.top.findNode("radioGrid")
    m.radioGrid.setFocus(true)
    m.radioGrid.observeField("selectedRadio", "onSelectedRadioChange")
end sub

sub onSelectedRadioChange()
    m.getRadiosTask = createObject("roSGNode", "GetRadios")
    m.getRadiosTask.observeField("responseBody", "onResponseBodyLoaded")

    m.getRadiosTask.clientID = `7a2ca6c8`
    m.getRadiosTask.radioID = m.radioGrid.selectedRadio
    m.getRadiosTask.control = "RUN"
end sub

sub onResponseBodyLoaded()
    if not isValid(m.getRadiosTask.responseBody) then return
    selectedRadioGenre = LCase(m.getRadiosTask.responseBody.results[0].name)

    m.getTracksTask = createObject("roSGNode", "GetTracks")
    m.getTracksTask.observeField("responseBody", "onTracksLoaded")

    m.getTracksTask.params = {
        client_id: `7a2ca6c8`,
        format: "jsonpretty",
        limit: 200,
        ' featured: 1,
        ' speed: "veryhigh",
        type: "single+albumtrack",
        tags: selectedRadioGenre
    }
    m.getTracksTask.control = "RUN"
end sub

sub onTracksLoaded()
    if not isValid(m.getTracksTask.responseBody) then return
    if not isValid(m.getTracksTask.responseBody.results) then return
    if m.getTracksTask.responseBody.results.count() = 0 then return

    audioContent = createObject("RoSGNode", "ContentNode")

    ' Create an array of all possible song indexes
    indexList = []
    for i = 0 to m.getTracksTask.responseBody.results.count() - 1
        indexList.push(i)
    end for

    ' Shuffle song indexes
    for i = 0 to indexList.count() - 1
        j = Rnd(indexList.count() - 1)
        temp = indexList[i]
        indexList[i] = indexList[j]
        indexList[j] = temp
    end for

    ' Add song content in shuffled order
    for each songIndex in indexList
        song = m.getTracksTask.responseBody.results[songIndex]
        songContent = createObject("RoSGNode", "ContentNode")
        songContent.ContentType = 5
        songContent.url = api.tracks.file(`7a2ca6c8`, song.id)
        songContent.title = decode(song.name)
        songContent.streamformat = "mp3"
        songContent.ShortDescriptionLine1 = decode(song.artist_name)
        songContent.album = decode(song.album_name)
        songContent.FHDPosterUrl = song.image
        audioContent.appendChild(songContent)
    end for

    m.audioPlayer.content = audioContent
    m.audioPlayer.control = "play"
end sub

sub onStateChange()

    if LCase(m.audioPlayer.state) = "playing"
        currentSong = m.audioPlayer.content.getChild(m.audioPlayer.contentIndex)
        m.artist.text = currentSong.ShortDescriptionLine1
        m.title.text = currentSong.title
        m.album.text = currentSong.album
        m.cover.uri = currentSong.FHDPosterUrl
        return
    end if

    m.artist.text = ""
    m.title.text = ""
    m.album.text = ""
    m.cover.uri = ""
end sub

function onKeyEvent(key as string, press as boolean) as boolean
    if not press then return false

    if key = "fastforward"
        m.audioPlayer.control = "skipcontent"
        return true
    end if

    return false
end function