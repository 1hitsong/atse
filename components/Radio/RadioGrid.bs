import "pkg:/source/utilities/utilities.bs"

sub init()
    getData()
end sub

sub getData()
    m.getRadiosTask = createObject("roSGNode", "GetRadios")
    m.getRadiosTask.observeField("responseBody", "onResponseBodyLoaded")

    m.getRadiosTask.limit = 50
    m.getRadiosTask.offset = 0
    m.getRadiosTask.control = "RUN"
end sub

sub onResponseBodyLoaded()
    m.getRadiosTask.unobserveField("responseBody")

    if not isValid(m.getRadiosTask.responseBody) then return

    data = CreateObject("roSGNode", "ContentNode")

    for each radio in m.getRadiosTask.responseBody.results

        ' For now, ignore problamatic radio stations
        if radio.id = 1 then continue for
        if radio.id > 13 then continue for

        gridAlbum = CreateObject("roSGNode", "ContentNode")

        gridAlbum.id = radio.name
        gridAlbum.shortdescriptionline1 = radio.dispname
        gridAlbum.HDGRIDPOSTERURL = radio.image
        gridAlbum.hdposterurl = radio.image
        gridAlbum.SDGRIDPOSTERURL = radio.image
        gridAlbum.sdposterurl = radio.image

        data.appendChild(gridAlbum)
    end for

    manualAlbum = CreateObject("roSGNode", "ContentNode")
    manualAlbum.id = "punk"
    manualAlbum.shortdescriptionline1 = "Punk Radio"
    manualAlbum.HDGRIDPOSTERURL = "pkg:/source/images/stations/punk.jpg"
    manualAlbum.hdposterurl = "pkg:/source/images/stations/punk.jpg"
    manualAlbum.SDGRIDPOSTERURL = "pkg:/source/images/stations/punk.jpg"
    manualAlbum.sdposterurl = "pkg:/source/images/stations/punk.jpg"
    data.appendChild(manualAlbum)

    m.top.content = data
end sub

function onKeyEvent(key as string, press as boolean) as boolean
    if not press then return false

    if key = "OK"
        m.top.selectedRadio = m.top.content.getChild(m.top.itemFocused).id
        return true
    end if

    return false
end function
